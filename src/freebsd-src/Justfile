help:
  @just -l

ccache +args:
  CCACHE_CONFIGPATH=$(realpath ccache.conf) ccache {{args}}

ccache-watch wait='5':
  while true; do clear; just ccache -s; sleep {{wait}}; done

clone:
  if [ ! -d main.jj ]; then jj git clone --colocate https://github.com/freebsd/freebsd-src.git main.jj; fi

bootstrap:
  doas make -C main.jj obj
  doas env CCACHE_CONFIGPATH=$(pwd)/ccache.conf make -C main.jj -j$(sysctl -n hw.ncpu) -s __MAKE_CONF=/dev/null SRCCONF=$(pwd)/src.conf buildworld buildkernel

build:
  doas env CCACHE_CONFIGPATH=$(pwd)/ccache.conf make -C main.jj -j$(sysctl -n hw.ncpu) -s -DWORLDFAST -DKERNFAST __MAKE_CONF=/dev/null SRCCONF=$(pwd)/src.conf buildworld buildkernel

build-dev *args:
  jj -R ../../repos/freebsd-src.jj show > dev-head
  plz build :rel--dev {{args}}

[no-cd]
format-patch change="":
  #!/bin/sh
  set -eu
  commit=$(jj -R ../../repos/freebsd-src.jj show {{change}} -T 'commit_id ++ "\n"' -s | head -n 1)
  git -C ../../repos/freebsd-src.jj format-patch -o $(pwd)/patches -U99999 ${commit}^..${commit}
